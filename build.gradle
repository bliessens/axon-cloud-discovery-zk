buildscript {
    ext {
        springBootVersion = '1.5.18.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "se.transmode.gradle:gradle-docker:1.2"
        classpath "io.spring.gradle:dependency-management-plugin:0.5.6.RELEASE"
    }
}


apply plugin: 'docker'

ext {
    axon = '3.4.2'
}


allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    ext {
        springCloudVersion = 'Edgware.SR4'
//    springCloudNetlixVersion = '1.3.6.RELEASE'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
//        mavenBom "org.springframework.cloud:spring-cloud-netflix:${springCloudNetlixVersion}"
        }
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    idea {
        module {
            downloadSources = true
            downloadJavadoc = true

            outputDir = file("$rootDir/out/production/$name")
            testOutputDir = file("$rootDir/out/test/$name")

            excludeDirs += [file('.gradle'), file('.idea'), file('build'), file('gradle')]

        }
    }
}

dependencies {

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'

    if (discovery == 'zookeeper') {
        compile project(':cloud:zookeeper')
    } else if (discovery == 'eureka') {
        compile project(':cloud:eureka')
    }

//axon
    compile "org.axonframework:axon-spring-boot-starter:$axon"

    testCompile "org.axonframework:axon-test:$axon"

    runtime 'org.hsqldb:hsqldb:2.3.6'
//	compile('org.springframework.kafka:spring-kafka')

    testCompile 'org.springframework.boot:spring-boot-starter-test'

    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
}

task ideaDockerPrepare(dependsOn: assemble) {
    doFirst {
        copy {
            from jar
            rename { fileName -> fileName.replace(jar.archiveName, 'axon-cloud-discovery.jar') }
            into 'build/libs'
        }
    }
}

task buildDocker(type: Docker, dependsOn: assemble) {
    applicationName = jar.baseName
    dockerfile = file('src/Dockerfile')
    doFirst {
        copy {
            from jar
            rename { fileName -> fileName.replace(jar.archiveName, 'axon-cloud-discovery.jar') }
            into stageDir
        }
    }
}

